
buildscript {
	ext {
		kotlinVersion = '1.3.21'
		springBootVersion = '2.1.3.RELEASE'
		springCloudVersion = 'Greenwich.RELEASE'
	}

	repositories {
		mavenCentral()
		jcenter()
	}
}

plugins {
	// publish build scans to https://scans.gradle.com by running `gradle build --scan`
	id 'com.gradle.build-scan' version '2.2.1'
	// https://kotlinlang.org/docs/reference/using-gradle.html#targeting-the-jvm
	id 'org.jetbrains.kotlin.jvm' version '1.3.21' apply false
	// https://kotlinlang.org/docs/reference/compiler-plugins.html#spring-support
	id 'org.jetbrains.kotlin.plugin.spring' version '1.3.21' apply false
    // https://kotlinlang.org/docs/reference/compiler-plugins.html#jpa-support
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.21' apply false
	// https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/html/
	id 'org.springframework.boot' version '2.1.3.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.7.RELEASE' apply false
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}

allprojects {
	group = 'by.naxa.learning'
	version = '1.0-SNAPSHOT'

	apply plugin: 'idea'
	apply plugin: 'eclipse'

	repositories {
		mavenCentral()
		jcenter()
	}
}

subprojects {

    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

	compileKotlin {
		kotlinOptions {
			freeCompilerArgs = ["-Xjsr305=strict"]
			languageVersion = "1.3"
			jvmTarget = "1.8"
		}
	}
	compileTestKotlin {
		kotlinOptions {
			freeCompilerArgs = ["-Xjsr305=strict"]
			languageVersion = "1.3"
			jvmTarget = "1.8"
		}
	}

	dependencies {
		implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
		implementation('org.jetbrains.kotlin:kotlin-reflect')
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
}
