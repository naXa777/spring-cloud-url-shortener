
buildscript {
	ext {
		kotlinVersion = '1.6.0'
		springBootVersion = '2.6.0'
		springCloudVersion = '2020.0.4'
	}

	repositories {
		mavenCentral()
	}
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}" apply false
	id 'org.jetbrains.kotlin.plugin.spring' version "${kotlinVersion}" apply false
	id 'org.jetbrains.kotlin.plugin.jpa' version "${kotlinVersion}" apply false
	id 'org.springframework.boot' version "${springBootVersion}" apply false
	id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

allprojects {
	group = 'by.naxa.learning'
	version = '1.0-SNAPSHOT'

	apply plugin: 'idea'
	apply plugin: 'eclipse'

	repositories {
		mavenCentral()
	}
}

subprojects {

	apply plugin: 'org.jetbrains.kotlin.jvm'
	apply plugin: 'org.jetbrains.kotlin.plugin.jpa'
	apply plugin: 'org.jetbrains.kotlin.plugin.spring'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	bootJar {
		launchScript()
	}

	compileKotlin {
		kotlinOptions {
			freeCompilerArgs = ["-Xjsr305=strict"]
			languageVersion = "1.6"
			jvmTarget = "11"
		}
	}
	compileTestKotlin {
		kotlinOptions {
			freeCompilerArgs = ["-Xjsr305=strict"]
			languageVersion = "1.6"
			jvmTarget = "11"
		}
	}

	dependencies {
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}")
		implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
}
