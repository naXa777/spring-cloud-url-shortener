
buildscript {
	ext {
		kotlinVersion = '1.3.21'
		springBootVersion = '2.1.3.RELEASE'
		springCloudVersion = 'Greenwich.RELEASE'
	}

	repositories {
		mavenCentral()
		jcenter()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
		classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
	}
}

plugins {
	// publish build scans to https://scans.gradle.com by running `gradle build --scan`
	id 'com.gradle.build-scan' version '2.2.1'
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}

allprojects {
	group = 'by.naxa.learning'
	version = '1.0-SNAPSHOT'

	apply plugin: 'idea'
	apply plugin: 'eclipse'

	repositories {
		mavenCentral()
		jcenter()
	}
}

subprojects {

	apply plugin: 'kotlin'
	apply plugin: 'kotlin-spring'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	compileKotlin {
		kotlinOptions {
			freeCompilerArgs = ["-Xjsr305=strict"]
			jvmTarget = "1.8"
		}
	}
	compileTestKotlin {
		kotlinOptions {
			freeCompilerArgs = ["-Xjsr305=strict"]
			jvmTarget = "1.8"
		}
	}

	dependencies {
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
		implementation("org.jetbrains.kotlin:kotlin-reflect")
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}
}
